# Maintainer: Jeff M. Hubbard <jeffmhubbard@gmail.com>
pkgbase=penguin-system
pkgname=(
    'penguin-base'
    'penguin-desktop'
    'penguin-desktop-scripts'
    'penguin-desktop-themes'
)
pkgver=16
pkgrel=2
pkgdesc="The Penguin Project"
arch=('any')
url="https://penguin.fyi/"
license=('MIT')

source=(
    'autostart-numlockx'
    'autostart-pcmanfm'
    'autostart-picom'
    'autostart-polkit'
    'autostart-unclutter'
    'autostart-update-notify'
    'autostart-user-dirs'
    'config-sudoers'
    'LICENSE'
    'patch-grub-distro'
    'patch-issue-ascii'
    'patch-makepkg.conf'
    'patch-pacman.conf'
    'patch-reflector.conf'
    'patch-useradd-shell'
    'penguin-base.install'
    'penguin-config-patch.hook'
    'penguin-config-patch.sh'
    'penguin-desktop.install'
    'README.md'
    'script-screen-capture'
    'script-touchpad-toggle'
    'script-update-notify'
)

package_penguin-base(){
    pkgdesc="Penguin base system"
    depends=(base curl efibootmgr fail2ban git gnupg
             grub linux-firmware man-db man-pages
             networkmanager openssh pass patch pwgen
             reflector sudo tlp tmux ufw unzip vi vim
             w3m zip zsh)
    install=penguin-base.install

    install -dm0750 "$pkgdir"/etc/sudoers.d
    install -Dm0644 config-sudoers "$pkgdir"/etc/sudoers.d/10-wheel

    install -Dm0644 patch-grub-distro "$pkgdir"/usr/share/penguin/patches/grub.patch
    install -Dm0644 patch-issue-ascii "$pkgdir"/usr/share/penguin/patches/issue.patch
    install -Dm0644 patch-makepkg.conf "$pkgdir"/usr/share/penguin/patches/makepkg.conf.patch
    install -Dm0644 patch-pacman.conf "$pkgdir"/usr/share/penguin/patches/pacman.conf.patch
    install -Dm0644 patch-reflector.conf "$pkgdir"/usr/share/penguin/patches/reflector.conf.patch
    install -Dm0644 patch-useradd-shell "$pkgdir"/usr/share/penguin/patches/useradd.patch

    install -Dm0644 penguin-config-patch.hook "$pkgdir"/usr/share/libalpm/hooks/90-penguin-config-patch.hook
    install -Dm0755 penguin-config-patch.sh "$pkgdir"/usr/share/libalpm/scripts/penguin-config-patch

    install -Dm0644 README.md "$pkgdir"/usr/share/penguin/README.md
    install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/penguin/LICENSE
}

package_penguin-desktop(){
    pkgdesc="Penguin Desktop software"
    depends=(penguin-base
             alacritty awesome-git awesome-freedesktop-git
             blueman fontconfig dex feh ffmpeg gvfs htop
             imagemagick lightdm lightdm-gtk-greeter
             lightdm-gtk-greeter-settings light-locker
             lxappearance-gtk3 mailcap mpc mpd mpv ncmpcpp
             neomutt netsurf network-manager-applet
             newsboat numlockx pasystray pavucontrol
             pcmanfm-gtk3 picom polkit-gnome pulseaudio
             pulseaudio-alsa qt5ct qt5-styleplugins
             qutebrowser ranger rofi tuir unclutter-xfixes-git
             urlscan weechat xarchiver xclip xdg-user-dirs
             xdg-utils xfce4-power-manager xorg-server
             xorg-xset xorg-xsetroot xorg-xrdb xorg-xrandr
             xorg-xdpyinfo xterm zathura zathura-pdf-mupdf)
    install=penguin-desktop.install

    install -Dm0644 autostart-numlockx "$pkgdir"/etc/xdg/autostart/numlockx.desktop
    install -Dm0644 autostart-pcmanfm "$pkgdir"/etc/xdg/autostart/pcmanfm.desktop
    install -Dm0644 autostart-picom "$pkgdir"/etc/xdg/autostart/picom.desktop
    install -Dm0644 autostart-polkit "$pkgdir"/etc/xdg/autostart/polkit-gnome.desktop
    install -Dm0644 autostart-unclutter "$pkgdir"/etc/xdg/autostart/unclutter.desktop
    install -Dm0644 autostart-user-dirs "$pkgdir"/etc/xdg/autostart/xdg-user-dirs.desktop
}

package_penguin-desktop-scripts(){
    pkgdesc="Penguin Desktop scripts"
    depends=(mpnotd-zsh walsh rofi-pass rofi-calc clipmenu maim)

    install -Dm0644 autostart-update-notify "$pkgdir"/etc/xdg/autostart/update-notify.desktop

    install -Dm0755 script-screen-capture "$pkgdir"/usr/bin/screen-capture
    install -Dm0755 script-touchpad-toggle "$pkgdir"/usr/bin/touchpad-toggle
    install -Dm0755 script-update-notify "$pkgdir"/usr/bin/update-notify
}

package_penguin-desktop-themes(){
    pkgdesc="Penguin Desktop themes"
    depends=(antsy-gtk-theme antsy-icon-theme antsy-wallpaper
             nerd-fonts-hack noto-fonts-emoji xcursor-neutral)
}

md5sums=('46868ac0617a4fbdfc308cc884d1f037'
         '752af9301a1fea60bb20f6979d272861'
         '3f9e6a050a80a07a29494117dce2d3de'
         '6529d3ddd2c2a6dff2845c57807ff058'
         '274e6a795ae6d5c77f7d4f61707cb113'
         '33fbf0ba82c9dd2abe34e616b3ae3ff8'
         '1797448eebfde434e6a796f5146342fe'
         '25b151ccac832c4b8333222106d16b8c'
         'a55d1384569349075b5a6a5c61af5c17'
         '41440c82d4b6c2d64e496ad4a1782778'
         '044f00bcb352b273e841ecbf3836b705'
         'c982fab7e21e835cfd6848f86657a22f'
         '257f3fd773cfac35ed40e2d5f885a72f'
         '08776fecbc01ca790ca20838943b0897'
         '79c690fc4d1faf1aa317cbe68246097f'
         '65800f60c55f2bdd4152b9c7003c045e'
         '08e127a575cc08cab68ed069a86cf5d6'
         'cdb56c7933f12c4b69180abba141ab5d'
         '7c47a6806061ccae3f5484973a00a0c6'
         '53f053dcc6d70532573578c60db0b279'
         'c73c5fa56f6bd1ad6b4e0b5ee90ccd6e'
         '41ddb2af63897120737119aaf45c9485'
         'eb39c2dcdeb05524ba23729fac9120e0')

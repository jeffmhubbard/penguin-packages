# Maintainer: Jeff M. Hubbard <jeffmhubbard@gmail.com>
pkgbase=penguin-system
pkgname=(
    'penguin-base'
    'penguin-desktop'
    'penguin-desktop-defaults'
    'penguin-desktop-scripts'
    'penguin-desktop-themes'
)
pkgver=13
pkgrel=4
pkgdesc="The Penguin Project"
arch=('any')
url="https://penguin.fyi/"
license=('MIT')

source=(
    'autostart-numlockx'
    'autostart-pcmanfm'
    'autostart-picom'
    'autostart-polkit'
    'autostart-unclutter'
    'autostart-update-notify'
    'autostart-user-dirs'
    'config-alacritty'
    'config-awesome'
    'config-awesome-theme'
    'config-dircolors'
    'config-gtk2'
    'config-gtk3'
    'config-picom'
    'config-qt5ct'
    'config-qutebrowser'
    'config-pcmanfm'
    'config-roficonfig'
    'config-rofitheme'
    'config-sudoers'
    'config-tmuxconf'
    'config-vimrc'
    'config-xresources'
    'config-xsession'
    'config-zshrc'
    'config-zshrc-prompt'
    'LICENSE'
    'patch-grub-distro'
    'patch-issue-ascii'
    'patch-lightdm-gtk-greeter.conf'
    'patch-makepkg.conf'
    'patch-pacman.conf'
    'patch-reflector.conf'
    'patch-useradd-shell'
    'penguin-base.install'
    'penguin-config-patch.hook'
    'penguin-config-patch.sh'
    'penguin-desktop.install'
    'README.md'
    'script-screen-capture'
    'script-touchpad-toggle'
    'script-update-notify'
)

package_penguin-base(){
    pkgdesc="Penguin base system"
    depends=(
        base linux-firmware grub efibootmgr tlp
        networkmanager openssh ufw fail2ban reflector
        zsh vim vi curl tmux git patch sudo man-db man-pages w3m
    )
    install=penguin-base.install

    install -dm0750 "$pkgdir"/etc/sudoers.d
    install -Dm0644 config-sudoers "$pkgdir"/etc/sudoers.d/10-wheel

    install -Dm0644 patch-grub-distro "$pkgdir"/usr/share/penguin/patches/grub.patch
    install -Dm0644 patch-issue-ascii "$pkgdir"/usr/share/penguin/patches/issue.patch
    install -Dm0644 patch-makepkg.conf "$pkgdir"/usr/share/penguin/patches/makepkg.conf.patch
    install -Dm0644 patch-pacman.conf "$pkgdir"/usr/share/penguin/patches/pacman.conf.patch
    install -Dm0644 patch-reflector.conf "$pkgdir"/usr/share/penguin/patches/reflector.conf.patch
    install -Dm0644 patch-useradd-shell "$pkgdir"/usr/share/penguin/patches/useradd.patch

    install -Dm0644 penguin-config-patch.hook "$pkgdir"/usr/share/libalpm/hooks/90-penguin-config-patch.hook
    install -Dm0755 penguin-config-patch.sh "$pkgdir"/usr/share/libalpm/scripts/penguin-config-patch

    install -Dm0644 README.md "$pkgdir"/usr/share/penguin/README.md
    install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/penguin/LICENSE
}

package_penguin-desktop(){
    pkgdesc="Penguin desktop software"
    depends=(
        penguin-base
        xorg-server xorg-xset xorg-xsetroot xorg-xrdb xorg-xrandr xorg-xdpyinfo
        lightdm lightdm-gtk-greeter lightdm-gtk-greeter-settings light-locker
        awesome-git awesome-freedesktop-git dex xdg-user-dirs xdg-utils
        pcmanfm-gtk3 gvfs xarchiver picom rofi rofi-pass xclip numlockx
        network-manager-applet blueman polkit-gnome xfce4-power-manager
        pulseaudio pulseaudio-alsa pavucontrol pasystray unclutter-xfixes-git
        lxappearance-gtk3 qt5ct qt5-styleplugins fontconfig walsh
        alacritty xterm ranger htop mailcap unzip zip pass pwgen gnupg
        qutebrowser netsurf weechat neomutt newsboat tuir neofetch
        mpd mpc ncmpcpp mpnotd-zsh mpv feh ffmpeg imagemagick urlscan maim
        zathura zathura-pdf-mupdf
    )
    install=penguin-desktop.install

    install -Dm0644 autostart-numlockx "$pkgdir"/etc/xdg/autostart/numlockx.desktop
    install -Dm0644 autostart-pcmanfm "$pkgdir"/etc/xdg/autostart/pcmanfm.desktop
    install -Dm0644 autostart-picom "$pkgdir"/etc/xdg/autostart/picom.desktop
    install -Dm0644 autostart-polkit "$pkgdir"/etc/xdg/autostart/polkit-gnome.desktop
    install -Dm0644 autostart-unclutter "$pkgdir"/etc/xdg/autostart/unclutter.desktop
    install -Dm0644 autostart-user-dirs "$pkgdir"/etc/xdg/autostart/xdg-user-dirs.desktop
}

package_penguin-desktop-defaults(){
    pkgdesc="Penguin default config files"
    depends=(
        penguin-desktop penguin-desktop-scripts penguin-desktop-themes
        zsh-autosuggestions zsh-completions zsh-syntax-highlighting bash-completion
    )

    install -Dm0644 config-alacritty "$pkgdir"/etc/skel/.config/alacritty/alacritty.yml
    install -Dm0644 config-awesome "$pkgdir"/etc/skel/.config/awesome/rc.lua
    install -Dm0644 config-awesome-theme "$pkgdir"/etc/skel/.config/awesome/themes/antsy/theme.lua
    install -Dm0644 config-dircolors "$pkgdir"/etc/skel/.dircolors
    install -Dm0644 config-gtk2 "$pkgdir"/etc/skel/.gtkrc-2.0
    install -Dm0644 config-gtk3 "$pkgdir"/etc/skel/.config/gtk-3.0/settings.ini
    install -Dm0644 config-picom "$pkgdir"/etc/skel/.config/picom/picom.conf
    install -Dm0644 config-pcmanfm "$pkgdir"/etc/skel/.config/pcmanfm/default/pcmanfm.conf
    install -Dm0644 config-qutebrowser "$pkgdir"/etc/skel/.config/qutebrowser/config.py
    install -Dm0644 config-qt5ct "$pkgdir"/etc/skel/.config/qt5ct/qt5ct.conf
    install -Dm0644 config-roficonfig "$pkgdir"/etc/skel/.config/rofi/config.rasi
    install -Dm0644 config-rofitheme "$pkgdir"/etc/skel/.config/rofi/themes/antsy.rasi
    install -Dm0644 config-tmuxconf "$pkgdir"/etc/skel/.tmux.conf
    install -Dm0644 config-vimrc "$pkgdir"/etc/skel/.vimrc
    install -Dm0644 config-xresources "$pkgdir"/etc/skel/.Xresources
    install -Dm0755 config-xsession "$pkgdir"/etc/skel/.xsession
    install -Dm0644 config-zshrc "$pkgdir"/etc/skel/.zshrc
    install -Dm0644 config-zshrc-prompt "$pkgdir"/etc/skel/.zshrc.prompt

    install -Dm0644 patch-lightdm-gtk-greeter.conf "$pkgdir"/usr/share/penguin/patches/lightdm-gtk-greeter.conf.patch
}

package_penguin-desktop-scripts(){
    pkgdesc="Penguin desktop scripts"
    optdepends=('mpnotd-zsh: mpd notifications')

    install -Dm0644 autostart-update-notify "$pkgdir"/etc/xdg/autostart/update-notify.desktop

    install -Dm0755 script-screen-capture "$pkgdir"/usr/bin/screen-capture.fyi
    install -Dm0755 script-touchpad-toggle "$pkgdir"/usr/bin/touchpad-toggle.fyi
    install -Dm0755 script-update-notify "$pkgdir"/usr/bin/update-notify.fyi
}

package_penguin-desktop-themes(){
    pkgdesc="Penguin (Antsy) themes"
    depends=(
        antsy-gtk-theme antsy-icon-theme antsy-wallpaper xcursor-openzone
        ttf-hack nerd-fonts-hack noto-fonts-emoji
    )
}

md5sums=('46868ac0617a4fbdfc308cc884d1f037'
         '752af9301a1fea60bb20f6979d272861'
         '3f9e6a050a80a07a29494117dce2d3de'
         '6529d3ddd2c2a6dff2845c57807ff058'
         '274e6a795ae6d5c77f7d4f61707cb113'
         '41fe7b2595064d3b2b8f595b286f125b'
         '1797448eebfde434e6a796f5146342fe'
         'd07c37d05a9bb5a89571ffa492bcd9bf'
         '06a6e487d21c72734e63b4be77c81545'
         'e07fb74fa812b198d32231ad49224ee6'
         'c198d7a057d3d5c5693932dd21243072'
         '2e99a6c2744cdcbe1075741806d6c62f'
         '1e4de6dbb641a532f47a62b8c4c76e68'
         '0c86433ee520ae3e4e3b62d46eafe7a7'
         '5c45a57289ab8192700ce374e94cad7a'
         '2c8fc63a49c9e475702818ad3896e006'
         '8d00616d31a7140a2f3b79ff22ed7201'
         '0229e09989d77e36316c52581fb7601d'
         'e45b4d51fb03251cc73bcfeb9a249163'
         '25b151ccac832c4b8333222106d16b8c'
         '117c57a1559599e1dababc55225a7bca'
         '0fc658d739f01daaa3c8c4043899db0a'
         '0ccbe35969f73c9bc670ce6e715f36b6'
         '99f6653f7007d98c8978024c10bb5660'
         '9100f9f66e5ea94dddc62227af839912'
         '7a22d5e966e64053fc4307c105431502'
         'a55d1384569349075b5a6a5c61af5c17'
         '41440c82d4b6c2d64e496ad4a1782778'
         '044f00bcb352b273e841ecbf3836b705'
         '2432e56f71823c95c6711c1634ab8005'
         'c982fab7e21e835cfd6848f86657a22f'
         '257f3fd773cfac35ed40e2d5f885a72f'
         '08776fecbc01ca790ca20838943b0897'
         '79c690fc4d1faf1aa317cbe68246097f'
         '65800f60c55f2bdd4152b9c7003c045e'
         '08e127a575cc08cab68ed069a86cf5d6'
         'cdb56c7933f12c4b69180abba141ab5d'
         '7c47a6806061ccae3f5484973a00a0c6'
         'fc439b5e6c1d7fbe5b30d1030c454ffc'
         'c73c5fa56f6bd1ad6b4e0b5ee90ccd6e'
         '41ddb2af63897120737119aaf45c9485'
         'eb25c430d8816615d9d0f0e23aac19d2')
